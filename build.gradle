import groovyx.net.http.HTTPBuilder
import groovyx.net.http.ContentType

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'geb.mobile'
version = new ProjectVersion(1, 0, System.env.BUILD_NUMBER)
ext.gebVersion = '0.10.0'
ext.seleniumVersion = '2.47.1'
ext.spockVersion = '1.0-groovy-2.3'
ext.groovyVersion = '2.3.10'
ext.selendroidVersion = '0.11.0'
ext.appiumClientVersion = '3.2.0'
ext.iosClientVersion =  '0.6+'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    selendroid
    iosdriver
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    configurations {
        all*.exclude module: 'selenium-chrome-driver'
        all*.exclude module: 'selenium-edge-driver'
        all*.exclude module: 'selenium-htmlunit-driver'
        all*.exclude module: 'selenium-firefox-driver'
        all*.exclude module: 'selenium-ie-driver'
        all*.exclude module: 'selenium-safari-driver'
    }
}

dependencies {

    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "org.slf4j:slf4j-api:1.7.6"
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    compile group: 'junit', name: 'junit', version: '4.11'


    compile("org.gebish:geb-spock:$gebVersion") {
        exclude module: "groovy"
        exclude module: "groovy-all"
    }
    compile("org.spockframework:spock-core:$spockVersion") {
        exclude module: "junit"
        exclude module: "groovy"
        exclude module: "groovy-all"
    }

    compile 'org.json:json:20150729'
    compile "io.selendroid:selendroid-client:$selendroidVersion"
    compile "org.uiautomation:ios-client:$iosClientVersion"
    compile "io.appium:java-client:$appiumClientVersion"

    integrationTestCompile group: 'junit', name: 'junit', version: '4.11'
    integrationTestCompile sourceSets.main.output
    integrationTestCompile configurations.testCompile
    integrationTestCompile sourceSets.test.output
    integrationTestRuntime configurations.testRuntime

    selendroid "io.selendroid:selendroid-standalone:${selendroidVersion}"

    iosdriver "org.uiautomation:ios-server:0.6+"
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file('build/classes/main')
        testOutputDir = file('build/classes/test')

        sourceDirs -= file('src/main/java')
        testSourceDirs -= file('src/test/java')

        testSourceDirs += file('src/integrationTest/groovy')
        testSourceDirs += file('src/integrationTest/resources')

        // This ended up causing issues when using the gradlew ideaModule approach vs. built in IntelliJ support
        // scopes.TEST.plus += [configurations.integrationTestCompile, configurations.integrationTestRuntime]

        // work around for gradle plugin being behind for IntelliJ 14
        iml.withXml {
            def resources = ['file://$MODULE_DIR$/src/main/resources']
            def testResources = ['file://$MODULE_DIR$/src/test/resources', 'file://$MODULE_DIR$/src/integrationTest/resources']

            it.asNode().component.content.sourceFolder?.each {
                def resourceType = null

                if (it.@url in resources) {
                    resourceType = 'java-resource'
                } else if (it.@url in testResources) {
                    resourceType = 'java-test-resource'
                }

                if (resourceType) {
                    def attributes = it.attributes()
                    attributes.remove('isTestSource')
                    attributes.put('type', resourceType)
                }
            }
        }
    }
}

def startServer = { server, command ->
    if( System.properties.'skipServer')  { println "Skipping Server $server "; return }
    println "Starting $server with $command"
    ProcessBuilder builder = new ProcessBuilder(command.split(' '))
    builder.redirectErrorStream(true)
    builder.directory(buildDir)
    Process process = builder.start()
    project.ext[server] = process
    println "$server started : $process"
    def loggingThread = Thread.start("logging-$server") {
        FileOutputStream fout = new FileOutputStream(new File(buildDir, "${server}.log"))
        int bb
        while ((bb = process.getInputStream().read()) > 0) {
            fout.write(bb)
        }
    }
    if(System.getProperty("waitForServer")){
        println "Joining Server Logging Thread"
        loggingThread.join()
    }
}

def stopServer = { server ->
    println "Stopping $server"
    if (project.ext.has(server)) {
        Process process = ext.get(server)
        println "send CTRL-C to server"
        try {
            process.getOutputStream().write(3)
            process.getOutputStream().flush()
        }catch(e){ }
        process.destroy()
    } else {
        println "No Server $server found in process list"
    }
}

task startSelendroid(group:'Server', type: Task) << {
    description 'Start the selendroid server on port 4444 with the testapp, use -DwaitForServer=true, when you run only the server'
    startServer( "SERVER_SELENDROID" , "java -cp ${configurations.selendroid.files.toList().join(":")} io.selendroid.SelendroidLauncher -port 4444 -aut ${projectDir}/src/integrationTest/resources/testapk/selendroid-test-app-0.9.0.apk")
}

task startSelendroidForWeb(group:'Server') << {
    startServer( "SERVER_SELENDROID_WEB" , "java -cp ${configurations.selendroid.files.toList().join(":")} io.selendroid.SelendroidLauncher -port 4444")
}

task startAppium(group:'Server') << {
    startServer("SERVER_APPIUM", "appium --command-timeout 90000")
}

task startIosdriver(group:'Server') << {
    startServer( "SERVER_IOSDRIVER", "java -Djava.util.logging.config.file=${projectDir}/config/iosdriver_logging.properties -cp ${configurations.iosdriver.files.toList().join(":")} org.uiautomation.ios.server.IOSServer -beta -simulators" )
}

task stopServers(group:'Server') << {
    project.ext.properties.each{k,v ->
        if( k=~/^SERVER_/ && project.ext.has(k) ) stopServer(k)
    }
}

task runSelendroidTests(group:'Android',type: Test, dependsOn: [startSelendroid]) {
    systemProperties.put "framework", "selendroid"
    systemProperties.put "appUT_absolutePath", "${projectDir}/src/integrationTest/resources/testapk/selendroid-test-app-0.9.0.apk"
    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appUT.version", "0.10.0"
    systemProperties.put "appUT_cap_Emulator", "false"
    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task runAppiumTests(group:'Android',type: Test, dependsOn: [startAppium]) {
    systemProperties.put "framework", "appium"

    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appium_app", "${projectDir}/src/integrationTest/resources/testapk/selendroid-test-app-0.9.0.apk"
    systemProperties.put "appium_deviceName" , "Android"
    //To run the test on a specific device
    //System.setProperty("appium_udid","192.168.56.10:5555")
    //System.setProperty("appium_automationName","selendroid")

    //Use Appium
    include '**/UIAutomatorNavigatorTestWithPagesSpec.*'
}

task runAppiumTestsWithSelendroidDriver(group:'Android', type: Test, dependsOn: [startAppium]) {
    systemProperties.put "framework", "appium"
    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appium_app", "${projectDir}/src/integrationTest/resources/testapk/selendroid-test-app-0.9.0.apk"
    systemProperties.put "appium_automationName", "selendroid"

    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task runAppiumTestsWithOldDevice(group:'Android', type: Test, dependsOn: [startAppium]) {
    systemProperties.put "framework", "appium"
    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appium_app", "${projectDir}/src/integrationTest/resources/testapk/selendroid-test-app-0.9.0.apk"

    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task runIosdriverTests(group: 'Ios', type: Test, dependsOn: [startIosdriver] ) {
    systemProperties.put "appUT.package", "UICatalog"
    systemProperties.put "iosdriver_app" , ""
    systemProperties.put "framework" , "iosdriver"
    include '**/UICatalogAppSpec.*'
}

task runIosdriverInitTests(group:'Ios', type:Test, dependsOn: [startIosdriver]){
    include '**/SimpleIosMobileTest.*'
}

task runIosdriverInitTestsReal(group:'Ios', type:Test, dependsOn: [startIosdriver]){
    include '**/SimpleIosMobileTest.*'
}


tasks.withType(Test){ task->
    onlyIf { !Boolean.getBoolean('skip.tests') }
    ignoreFailures = true
    systemProperties.put = System.properties
    outputs.upToDateWhen{ false }
    doLast{
        stopServers.execute()
    }
}

task checkenv << {
    if( System.properties.'os.name'.toLowerCase().startsWith("mac") ){
        println "MAC ... "
        println "check appium installation: "+ 'appium -v'.execute().text

    }
}

ext.sauceLabsUser = this.hasProperty("sauceLabsUser") ? sauceLabsUser : System.getProperty("sauceLabsUser", System.getenv("SAUCE_USERNAME"))
ext.sauceLabsKey  = this.hasProperty("sauceLabsKey") ? sauceLabsKey : System.getProperty("sauceLabsKey", System.getenv("SAUCE_ACCESS_KEY"))

println "sauceLabsUser: ${ext.sauceLabsUser}"
println "sauceLabsKey: ${ext.sauceLabsKey}"

ext.saucelabsUrl  = "http://${sauceLabsUser}:${sauceLabsKey}@ondemand.saucelabs.com:80/wd/hub"

task appiumOnSauceLabs(type: Test) {
    systemProperty "framework","appium"
    systemProperty "appium_app","sauce-storage:selendroid-test-app-0.9.0.apk"
    systemProperty "appium_appPackage" , "io.selendroid.testapp"
    systemProperty "selenium.url", saucelabsUrl
    systemProperty "appium_platformName","Android"
    systemProperty "appium_deviceName","LG Nexus 4 Emulator"
    systemProperty "appium_platformVersion","4.4"
    systemProperty "appium_appium-version","1.3.7"
    include '**/UIAutomatorNavigatorTestWithPagesSpec.*'
}

task uploadApk2Saucelabs(){
    def appFile = new File("${projectDir}/src/integrationTest/resources/testapk/selendroid-test-app-0.9.0.apk")
    inputs.file appFile
    outputs.upToDateWhen { true }

    doLast {
        String sauceStorageUrl = "https://${sauceLabsUser}:${sauceLabsKey}@saucelabs.com/rest/v1/storage/${sauceLabsUser}/selendroid-test-app-0.9.0.apk?overwrite=true"
        logger.lifecycle "Uploading apk to sauce storage at: ${sauceStorageUrl}"
        def sauceStorage = new HTTPBuilder(sauceStorageUrl)
        sauceStorage.handler.failure = { resp ->
            logger.error "  unexpected failure: ${resp.statusLine}"
        }

        sauceStorage.client.params?.with {
            int timeout = 120 * 1000
            setParameter("http.connection.timeout", timeout)
            setParameter("http.socket.timeout", timeout)
        }

        try {
            logger.lifecycle "uploading appfile size:" + appFile?.bytes?.size()
            sauceStorage.post(requestContentType: ContentType.BINARY, body: appFile.bytes)
        } catch (Exception e) {
            e.printStackTrace()
            logger.lifecycle "trying to upload again"
            sauceStorage.post(requestContentType: ContentType.BINARY, body: appFile.bytes)
        }
    }
}

ext.testobjectApiKey = System.getProperty("testobjectApiKey", System.getenv("TESTOBJECT_APIKEY"))
ext.testobjectUrl = System.getProperty("testobjectUrl", 'https://app.testobject.com:443/api/appium/wd/hub')

task appiumOnTestObjects(type: Test) {
    systemProperties.put "framework", "appium"
    systemProperties.put "selenium.url", testobjectUrl
    systemProperties.put "appium_appPackage" , "io.selendroid.testapp"       //important for testobject test, cause this value is not yet set on there side
    systemProperties.put 'appium_testobject_api_key', testobjectApiKey
    systemProperties.put 'appium_testobject_project', 'selendroid-test-app'
    systemProperties.put 'appium_testobject_app_id', '1'
    systemProperties.put 'appium_testobject_device', 'LG_Nexus_4_E960_real'
    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

[
        runSelendroidTests,
        runAppiumTests,
        runAppiumTestsWithSelendroidDriver,
        runAppiumTestsWithOldDevice,
        runIosdriverTests,
        runIosdriverInitTests,
        appiumOnTestObjects,
        appiumOnSauceLabs,
        runIosdriverInitTestsReal
].each {
    it.testClassesDir = sourceSets.integrationTest.output.classesDir
    it.classpath = sourceSets.integrationTest.runtimeClasspath

    it.beforeTest { descriptor ->
        logger.lifecycle "Running test: $descriptor"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'geb-mobile'

            from components.java
        }
    }

    repositories {
        maven {
            url = version.toString().endsWith('SNAPSHOT') ? nexusSnapshotUrl : nexusReleaseUrl
            credentials {
                username = nexusUser
                password = nexusPassword
            }
        }
    }
}

// TODO:  figure out how to do this?
//apply from: '../gradle/versioning.gradle'

// start versioning.gradle
ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

class ProjectVersion {
    Integer major
    Integer minor
    String build

    ProjectVersion(Integer major, Integer minor, String build) {
        this.major = major
        this.minor = minor
        this.build = build
    }

    String toString() {
        String fullVersion = "$major.$minor"

        if (build) {
            fullVersion += ".$build"
        } else {
            fullVersion += "-SNAPSHOT"
        }

        fullVersion
    }
}
// stop versioning.gradle
